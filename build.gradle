plugins {
    id 'java-gradle-plugin'
    id 'groovy'
    id 'com.gradle.plugin-publish' version '0.18.0'
    id "de.marcphilipp.nexus-publish" version "0.4.0"
    id "io.codearte.nexus-staging" version "0.21.1"
}


wrapper {
    gradleVersion = "${project.property('gradle.wrapper.version')}"
    distributionType = Wrapper.DistributionType.ALL
}

ext {
    user = 'jprante'
    name = 'gradle-plugin-shadow'
    pluginDescription = 'Gradle plugin for shadow jars'
    scmUrl = 'https://github.com/' + user + '/' + name
    scmConnection = 'scm:git:git://github.com/' + user + '/' + name + '.git'
    scmDeveloperConnection = 'scm:git:git://github.com/' + user + '/' + name + '.git'
}

apply plugin: 'java-library'
apply from: rootProject.file('gradle/ide/idea.gradle')
apply from: rootProject.file('gradle/compile/java.gradle')
apply from: rootProject.file('gradle/compile/groovy.gradle')
apply from: rootProject.file('gradle/test/junit5.gradle')
apply from: rootProject.file('gradle/publishing/sonatype.gradle')

dependencies {
    api gradleApi()
    implementation "org.ow2.asm:asm:${project.property('asm.version')}"
    implementation "org.ow2.asm:asm-commons:${project.property('asm.version')}"
    implementation "org.ow2.asm:asm-util:${project.property('asm.version')}"
    testImplementation gradleTestKit()
    testImplementation "org.spockframework:spock-core:${project.property('spock.version')}"
    testImplementation "org.spockframework:spock-junit4:${project.property('spock.version')}"
}

validatePlugins {
    failOnWarning = false
}

gradlePlugin {
    plugins {
        shadowPlugin {
            id = 'org.xbib.gradle.plugin.shadow'
            implementationClass = 'org.xbib.gradle.plugin.shadow.ShadowPlugin'
        }
    }
}

if (project.hasProperty('gradle.publish.key')) {
    pluginBundle {
        website = scmUrl
        vcsUrl = scmUrl
        plugins {
            shadowPlugin {
                id = 'org.xbib.gradle.plugin.shadow'
                version = project.version
                description = 'Shadow plugin for Gradle'
                displayName = 'Shadow plugin for Gradle'
                tags = [ 'shadow' ]
            }
        }
    }
}
sourceCompatibility = JavaVersion.VERSION_1_9
targetCompatibility = JavaVersion.VERSION_1_9
